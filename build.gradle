plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.yabets'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.5'
    implementation 'com.opencsv:opencsv:5.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
    implementation 'org.jline:jline:3.26.3'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'com.yabets.carprocessor.Main'
}

shadowJar {
    archiveBaseName.set('car-processor')
    archiveClassifier.set('')
    archiveVersion.set('1.0-SNAPSHOT')
    manifest {
        attributes(
                'Main-Class': 'com.yabets.carprocessor.Main'
        )
    }
    doFirst {
        logger.info 'Starting shadowJar task to generate car-processor-1.0-SNAPSHOT.jar'
    }
    doLast {
        logger.info "Generated JAR at ${archiveFile.get().asFile}"
    }
}

tasks.named('jar') {
    enabled = false // Disable default jar task to avoid conflicts
}

tasks.named('build') {
    dependsOn shadowJar
}

tasks.named('distZip') {
    dependsOn shadowJar
}

tasks.named('distTar') {
    inputs.files shadowJar
    dependsOn shadowJar
}

tasks.named('startScripts') {
    inputs.files shadowJar
    dependsOn shadowJar
}
